#+title: Underline with a char

* License

Copyright (c) Marco Wahl 2017

GPL 3

* About
:PROPERTIES:
:ID:       d1310a31-62ff-452f-b07b-312a17bf85b0
:END:

[[https://travis-ci.org/marcowahl/underline-with-char.svg?branch=master]]

When point is below a line then fill the line with a character making
it as long as the line above.

There are two functions

1. =underline-with-char= uses the internal setting for the fill-char.
2. =underline-with-char-choose= allows to choose the fill-char.

*** Examples

Notation: <!> marks point.

***** Full underlining

Input:

#+begin_src text
lala
<!>
#+end_src

Action:

#+begin_src text
M-x underline-with-char
#+end_src

Output:

#+begin_src text
lala
----<!>
#+end_src

***** Partial underlining

Input:

#+begin_src text
;; lolo
;; <!>
#+end_src

Action:

#+begin_src text
M-x underline-with-char
#+end_src

Output:

#+begin_src text
;; lolo
;; ----<!>
#+end_src

***** Use a certain char for underlining

Input:

#+begin_src text
lala
<!>
#+end_src

Action:

#+begin_src text
M-x underline-with-char-choose X
#+end_src

Output:

#+begin_src text
lala
XXXX<!>
#+end_src

***** Set a char for subsequent underlinings

Input:

#+begin_src text
lala
<!>
#+end_src

Action (Note the prefix argument.):

#+begin_src text
C-u M-x underline-with-char-choose X
RET
M-x underline-with-char
#+end_src

Output:

#+begin_src text
lala
XXXX
XXXX<!>
#+end_src

* Install

Load the elisp file.

* Build

This is a literate program.  Generating the pure program is called
tangling.  Concretely tangle with C-cvt in Emacs Orgmode.

*** For the very first stages

You might need to perform

#+begin_src shell
export CASK_EMACS=/usr/bin/emacs-25.2
cask init
cask install
cask exec ert-runner init
#+end_src

- [2017-06-21 Wed 12:14] tip version of Emacs did not work with cask install.
* Testing
:PROPERTIES:
:ID:       c960a64f-5dc8-463d-b7b5-48f3c1ff2a3d
:header-args:emacs-lisp: :tangle test/underline-with-char-test.el
:END:

The tests are style ert-runner (which needs cask IIUC.)

Run tests e.g. like

[[elisp:(compile "CASK_EMACS=/usr/bin/emacs-25.2 make test") ]] Recall
cask does not work well with current Emacs tip version I use normally.
This is the rationale for the setting CASK_EMACS to a suitable
version.

Get help with

#+begin_src shell
cask exec ert-runner help
#+end_src

- Note :: I think that the test file must end with "-test" with
          ert-runner default settings.

*** First line
:PROPERTIES:
:ID:       c3ab7721-53d9-4abe-a5e6-e031c4a9f5f1
:END:

#+begin_src emacs-lisp :padline no
;;; underline-with-char-test.el --- tests for underline-with-char.el  -*- lexical-binding: t ; eval: (view-mode 1) -*-
#+end_src

*** Concrete tests
:PROPERTIES:
:ID:       17c5897e-3413-4576-aa83-3869e0cb1053
:END:

#+begin_src emacs-lisp :comments both
(require 'underline-with-char)

(ert-deftest f2149b5d7e74f04715435e3767bb5b28eb973ab6 ()

  (should
   (equal
    "lala
----"
    (with-temp-buffer
      (let ((underline-with-char-fill-char ?-))
       (insert "lala
")
       (call-interactively #'underline-with-char)
       (buffer-substring (point-min) (point-max)))))))

(ert-deftest 1cf806ca781aba4a68e248c4a3c5a0bd3017ea75 ()
  (should
   (equal
    "lala
la--"
    (with-temp-buffer
      (let ((underline-with-char-fill-char ?-))
        (insert "lala
la")
        (call-interactively #'underline-with-char)
        (buffer-substring (point-min) (point-max)))))))

(ert-deftest 88bded2c526dcf44116420e7a33eb7ab58b905ee ()
  (should
   (string=
    "a
a"
    (with-temp-buffer
      (let ((underline-with-char-fill-char ?a))
        (insert "a
")
        (call-interactively #'underline-with-char))
        (buffer-substring (point-min) (point-max))))))

(ert-deftest eb74280b23db3a9ac18032d641aa280a6cb2c4c1 ()
  (should
   (equal
    "lala
    "
    (with-temp-buffer
      (insert "lala
")
      (underline-with-char-choose nil ? )
      (buffer-substring (point-min) (point-max))))))

(ert-deftest 69756cd95b0b4ad224f728d57799253664e1c79c ()
  (should
   (equal
    "lala
++++
++++"
    (with-temp-buffer
      (insert "lala
")
      (underline-with-char-choose '(4) ?+)
      (insert "\n")
      (underline-with-char)
      (buffer-substring (point-min) (point-max))))))
#+end_src
---------
+++++++++

*** Last line
:PROPERTIES:
:ID:       d37f9d32-541b-4a08-815e-394d858586d6
:END:
#+begin_src emacs-lisp
;;; underline-with-char-test.el ends here
#+end_src

* Code
:PROPERTIES:
:header-args:emacs-lisp: :tangle underline-with-char.el
:END:

*** First line
:PROPERTIES:
:ID:       c3ab7721-53d9-4abe-a5e6-e031c4a9f5f1
:END:

#+begin_src emacs-lisp :padline no
;;; underline-with-char.el --- Underline with a char  -*- lexical-binding: t ; eval: (view-mode 1) -*-

;; THIS FILE HAS BEEN GENERATED.

#+end_src

*** Program
:PROPERTIES:
:ID:       17c5897e-3413-4576-aa83-3869e0cb1053
:END:

#+begin_src emacs-lisp :comments both

;; THIS FILE HAS BEEN GENERATED.


;;; Commentary:

;; Version: 1.0.0
;; Package-Requires: ((emacs "24"))
;; Keywords: convenience

;; There are two functions
;;
;; 1. `underline-with-char' uses the internal setting for the fill-char.
;; 2. `underline-with-char-choose' allows to choose the fill-char.

;; E.g. with `underline-with-char-fill-char' set to '-' and point
;; symbolized as <!> and starting with
;;
;; ;; Worthy to be underlined
;; ;; <!>
;;
;; then
;;
;; M-x underline-with-char
;;
;; yields
;;
;; ;; Worthy to be underlined
;; ;; -----------------------

;; You can also set a character for the next underline using function
;; `underline-with-char-choose'.

;; Example
;; _______

;; ;; Worthy to be underlined
;; ;; <!>
;;
;; then
;;
;; M-x underline-with-char-choose _
;;
;; yields
;;
;; ;; Worthy to be underlined
;; ;; _______________________

;; You can also set the underline character for subsequent calls to `underline-with-char'.
;; Example
;; _______

;; Worthy to be underlined two times
;; <!>
;;
;; then
;;
;; C-u M-x underline-with-char-choose X
;; RET
;; M-x underline-with-char

;; yields
;;
;; Worthy to be underlined two times
;; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
;; XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX


;;; Code:


(defcustom underline-with-char-fill-char ?-
  "The character for the underline."
  :group 'underline-with-char
  :type 'character)


;;;###autoload
(defun underline-with-char ()
  "Underline the line above with a certain character.

The character is defined by `underline-with-char-fill-char'.

Fill what's remaining if not at the first position.

E.g. with `underline-with-char-fill-char' set to '-' and point
symbolized as <!> and starting with

;; Commentary:
;; <!>

get

;; Commentary:
;; -----------"
  (interactive)
  (underline-with-char-choose nil underline-with-char-fill-char))

;;;###autoload
(defun underline-with-char-choose (arg char)
  "Underline the line above with a certain character.

Fill what's remaining if not at the first position.

With prefix ARG use the CHAR for subsequent calls to
`underline-with-char'"
  (interactive  "P\ncchar: ")
  (insert
   (make-string
    (save-excursion
      (let ((col (current-column)))
        (forward-line -1)
        (end-of-line)
        (when (< col (current-column))
          (beginning-of-line)
          (forward-char col)))
      (let ((old-point (point)))
        (- (progn (end-of-line) (point)) old-point)))
    char))
    (if (equal '(4) arg)
      (setq underline-with-char-fill-char char)))


(provide 'underline-with-char)
#+end_src

*** Last line
:PROPERTIES:
:ID:       d37f9d32-541b-4a08-815e-394d858586d6
:END:
#+begin_src emacs-lisp


;;; underline-with-char.el ends here
#+end_src
